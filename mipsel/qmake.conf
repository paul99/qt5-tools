#
# qmake configuration for building with mipsel-linux-gnu-g++
#

MAKEFILE_GENERATOR      = UNIX
TARGET_PLATFORM         = unix
TEMPLATE                = app
CONFIG                  += qt warn_on release incremental link_prl gdb_dwarf_index
QT                      += core gui
QMAKE_INCREMENTAL_STYLE = sublib

include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/g++-unix.conf)

# modifications to g++.conf
QMAKE_CC                = mipsel-linux-gnu-gcc
QMAKE_CXX               = mipsel-linux-gnu-g++
QMAKE_LINK              = mipsel-linux-gnu-g++
QMAKE_LINK_SHLIB        = mipsel-linux-gnu-g++

# modifications to linux.conf
QMAKE_AR                = mipsel-linux-gnu-ar cqs
QMAKE_OBJCOPY           = mipsel-linux-gnu-objcopy
QMAKE_STRIP             = mipsel-linux-gnu-strip
QMAKE_CFLAGS           += -mips32r2
QMAKE_CXXFLAGS         += -mips32r2

# Use a variable which points to the install directory of QT
# This is required, because -rpath is only used for dependent shared 
# library resolution if the toolchain is --sysroot enabled and
# the Emdebian toolchain is not
QMAKE_LFLAGS += -Wl,-rpath-link=$$[QT_INSTALL_PREFIX]/lib

load(qt_config)
